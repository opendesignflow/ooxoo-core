buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
}

plugins {
    id 'scala'

    // Publish
    id 'maven-publish'
    id "java-library"

}

// Versions
//-----------------
group = 'org.odfi.ooxoo'
version = gradle.ext.has("version") ? gradle.ext.version : "dev"
def scalaVersion = "2.13.5"

// Sources
//---------------
sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala', 'src/main/java']
        }
        java {
            srcDirs = []
        }
    }
}

// Deps
//-----------
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}
dependencies {


    // ODFI Deps
    //--------------
    api "org.odfi:tea:3.4.2"

    // Common JAXB Stuff
    //---------------

    // Special Buffer types like HTML string, compresssing buffer
    api "org.apache.commons:commons-lang3:3.12.0"
    api "org.apache.commons:commons-compress:1.20"

    // Stax indenting output
    api "org.glassfish.jaxb:txw2:3.0.0"

    // Producer
    //--------------

    // Library to make words plurals
    api "org.atteo:evo-inflector:1.2.2"


    // JSON API and Impl for javax
    //----------------

    api "org.eclipse:yasson:1.0.9"
    //api 'javax.json:javax.json-api:2.0.0'
    //api "javax.json.bind:javax.json.bind-api:1.0.2"


    // GSON
    //---------------
    api 'com.google.code.gson:gson:2.8.6'


    // JPA Interfaces
    //-----------------
    api "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"
    // https://mvnrepository.com/artifact/javax.persistence/javax.persistence-api
   // api group: 'javax.persistence', name: 'javax.persistence-api', version: '3.0.0'
    api group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'

    // Scala
    //---------------
    api "org.scala-lang.modules:scala-parser-combinators_2.13:1.1.2"
    api "org.scala-lang:scala-reflect:$scalaVersion"
    api "org.scala-lang:scala-compiler:$scalaVersion"
    api("org.scala-lang:scala-library:$scalaVersion")

    testImplementation "org.scala-lang.modules:scala-xml_2.13:2.0.0-M3"
    testImplementation 'org.scalatest:scalatest-funsuite_2.13:3.2.6'
    testImplementation 'org.scalatest:scalatest-shouldmatchers_2.13:3.2.6'
}


repositories {

    mavenLocal()
    mavenCentral()
    maven {
        name = 'Sonatype Nexus Snapshots'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name = 'ODFI Releases'
        url = 'https://www.opendesignflow.org/maven/repository/internal/'
    }
    maven {
        name = 'ODFI Snapshots'
        url = 'https://www.opendesignflow.org/maven/repository/snapshots/'
    }
    google()
    jcenter()
}

publishing {
    publications {


        publishToMavenLocal(MavenPublication) {

            // groupId    group
            artifactId "ooxoo-core"
            //version    version

            from components.java

            pom {
                name = 'OOXOO Core'
                description = 'Core runtime for OOXOO'

                //properties = [ ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'richnou'
                        name = 'Richnou'
                        email = 'leys.richard@gmail.com'
                    }
                }

            }
        }

    }
    repositories {
        maven {

            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://www.opendesignflow.org/maven/repository/internal/"
            def snapshotsRepoUrl = "https://www.opendesignflow.org/maven/repository/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            // Credentials
            //-------------
            def mavenOdfiPropertiesFile = new File(System.getProperty("user.home") + File.separator + ".gradle/odfi-maven.properties").getCanonicalFile()

            if (mavenOdfiPropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(mavenOdfiPropertiesFile))

                credentials {
                    username keystoreProperties["user"]
                    password keystoreProperties["password"]
                }
            } else {
                credentials {
                    username System.getenv("PUBLISH_USERNAME")
                    password System.getenv("PUBLISH_PASSWORD")
                }
            }

        }
    }
}